apply plugin: 'idea'

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'idea'

    sourceCompatibility = 1.6

    repositories {
        mavenCentral()
    }
    configurations {
        optional // Like Maven's optional or provided dependencies
    }
    sourceSets {
        main {
            compileClasspath += configurations.optional
            runtimeClasspath += configurations.optional
        }
        test {
            compileClasspath += configurations.optional
            runtimeClasspath += configurations.optional
        }
    }
    dependencies {
        compile(
            'org.codehaus.groovy:groovy-all:2.1.1',
        )
        testCompile(
        )
    }
    idea {
        module {
            scopes.PROVIDED.plus += configurations.optional
        }
    }
}

project('server-builder-api') {
    dependencies {
        compile(
        )
        optional(
            // Jersey server support
            'com.sun.jersey:jersey-server:1.17',
            'javax.ws.rs:jsr311-api:1.1.1',
            // Spring MVC support
            'org.springframework:spring-web:3.2.1.RELEASE',
            // Optional jersey client for those who want the builder to provide a pre-configured client that can
            // talk to the built server
            'com.sun.jersey:jersey-client:1.17',
        )
        testCompile(
        )
    }
}

project('server-builder-core') {
    dependencies {
        compile(
            project(':server-builder-api'),
        )
        optional(
            'org.eclipse.jetty:jetty-servlet:8.1.9.v20130131',
            project(':server-builder-jetty7'),
            project(':server-builder-jetty8'),
        )
        testCompile(
        )
    }
}

project('server-builder-test-support') {
    dependencies {
        compile(
            project(':server-builder-api'),
            project(':server-builder-core'),
            'org.spockframework:spock-core:0.7-groovy-2.0',
            'com.sun.jersey:jersey-client:1.17', // Just a convenient HTTP client to test with
        )
        optional(
            // These are required to compile the test classes but mustn't interfere with the classpath of the module
            // under test. Each module should supply its own, correct dependencies.
            'org.springframework:spring-webmvc:3.2.1.RELEASE',
            'org.springframework:spring-web:3.2.1.RELEASE',
            'org.springframework:spring-context:3.2.1.RELEASE',
            'org.springframework:spring-beans:3.2.1.RELEASE',
            'com.sun.jersey:jersey-server:1.17',
            'com.sun.jersey:jersey-servlet:1.17',
            'com.sun.jersey:jersey-core:1.17',
            'javax.ws.rs:jsr311-api:1.1.1',
            'com.sun.jersey.contribs:jersey-spring:1.17', // Should probably be optional or maybe a separate module?
        )
        testCompile(
        )
    }
}

project('server-builder-jetty8') {
    dependencies {
        compile(
            project(':server-builder-api'),
            'org.eclipse.jetty:jetty-servlet:8.1.9.v20130131',
        )
        optional(
            'com.sun.jersey:jersey-client:1.17',
            // Spring MVC support
            'org.springframework:spring-webmvc:3.2.1.RELEASE',
            'org.springframework:spring-web:3.2.1.RELEASE',
            // Jersey server support
            'com.sun.jersey:jersey-server:1.17',
            'com.sun.jersey:jersey-servlet:1.17',
            'com.sun.jersey:jersey-core:1.17',
            'javax.ws.rs:jsr311-api:1.1.1',
            'com.sun.jersey.contribs:jersey-spring:1.17',
        )
        testCompile(
            project(':server-builder-test-support'),
        )
    }
}

project('server-builder-jetty7') {
    dependencies {
        compile(
            project(':server-builder-api'),
            'org.eclipse.jetty:jetty-server:7.6.10.v20130312',
            'org.eclipse.jetty:jetty-servlet:7.6.10.v20130312',
        )
        optional(
            // Jersey server support
            'com.sun.jersey:jersey-server:1.17',
            'com.sun.jersey:jersey-servlet:1.17',
            'com.sun.jersey:jersey-core:1.17',
            'javax.ws.rs:jsr311-api:1.1.1',
            'com.sun.jersey.contribs:jersey-spring:1.17',
            // Spring MVC support
            'org.springframework:spring-web:3.2.1.RELEASE',
            'org.springframework:spring-webmvc:3.2.1.RELEASE',
            'com.sun.jersey:jersey-client:1.17',
        )
        testCompile(
            project(':server-builder-test-support'),
        )
    }
}
